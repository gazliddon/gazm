;  NLIST                                                
;  INCLUDE	SPHR56.SRC                                                
;  INCLUDE	EQ4.SRC                                                
;  LIST                                                 
;  TTL	<<<<< S T A R G A T E  >>>>>                                                
        org LOGORG                                      
        JMP ATTR_                                       
        JMP GOV_                        ; GAME OVER     
        JMP HALL                                        
        JMP GNCIDA                                      
;                                                       
; ATTRACT MODE                                                
;                                                       
GOV_    equ *                                           
ATTR_   LDX #CHKS2+78                   ; HIDDEN CHECKSUM
        LDY #$D004                                      
        CLRB                                            
ATTR0_  ADDB ,Y                                         
        LEAY $9,Y                                       
        CMPY #$F000                                     
        BLO ATTR0_                                      
        STB -78,X                                       
        NOP                                             
        JSR GNCIDE                                      
        NEWP (CRDSCN,ATYPE)                                
        LDA ATRFLG                                      
        BNE HALLV                       ; LET HIM SEE THE HALL
        LDA CREDIT                                      
        LBNE CRDPAG                                     
HALLV   JMP HALL                                        
;                                                       
; KILL ALL BUT ATTR PROCESSES                                                
;                                                       
GNCIDA  PSHS A,X                                        
        LDX #ACTIVE                                     
GNC1    LDX ,X                                          
        BEQ GNCX                                        
        CMPX CRPROC                                     
        BEQ GNC1                                        
        LDA PTYPE,X                                     
        CMPA #CTYPE                                     
        BEQ GNC1                                        
        CMPA #ATYPE                                     
        BEQ GNC1                                        
        JSR KILL                                        
        BRA GNC1                                        
GNCX    PULS A,X,PC                                     
;                                                       
; CREDIT SCANNING                                                
;                                                       
CRDSCN  LDA CREDIT                                      
        STA PD,U                                        
        CLR PD1,U                                       
        CLR PD2,U                                       
CRDSLP  LDA CREDIT                                      
        CMPA PD,U                                       
        BEQ CRDSLX                                      
        STA PD,U                                        
        LDA ATRFLG                                      
        CMPA #$50                       ; CREDIT PAGE?? 
        BNE CRDSL1                                      
        JSR OCRDM                                       
        BRA CRDSLX                                      
CRDSL1  JSR GNCIDE                                      
        LDA STYPE                                       
        LDX #CRDPAG                                     
        JSR MSPROC                                      
CRDSLX  LDA PIA21                                       
        ANDA #$8                                        
        CMPA PD1,U                                      
        BEQ HALSX                                       
        STA PD1,U                                       
        BEQ HALSX                                       
        LDA ATRFLG                                      
        CMPA #$10                                       
        BEQ HALSX                                       
        JSR GNCIDE                                      
        LDA STYPE                                       
        LDX #HALL                                       
        JSR MSPROC                                      
HALSX   LDA PIA21                                       
        ANDA #$40                                       
        CMPA PD2,U                                      
        BEQ SECSX                                       
        STA PD2,U                                       
        BEQ SECSX                                       
        LDA ATRFLG                                      
        CMPA #$40                                       
        BEQ SECSX                                       
        JSR GNCIDE                                      
        LDA STYPE                                       
        LDX #SECRET                                     
        JSR MSPROC                                      
SECSX   NAP (2,CRDSLP)                                  
;                                                       
; CREDIT PAGE DISPLAY                                                
;                                                       
CRDPAG  LDA #$50                                        
        STA ATRFLG                                      
        LDA #$FF                                        
        STA STATUS                                      
        JSR GNCIDA                                      
        JSR SCRCLR                                      
        JSR CRINIT                                      
        JSR BORDER                                      
        JSR ONCOLV                                      
        MAKP (OPMCOL)                                   
        JSR OCRDM                                       
        LDU CRPROC                                      
        LDA CREDIT                                      
        STA PD4,U                                       
        LDA #2                                          
        STA PLRCNT                                      
        LDA #8                                          
        STA PD3,U                       ; TIMER         
        LDX #$14AC                                      
        JSR OSEXM                       ; OUTPUT EXTRA SHIP MESSAGE
        LDX #$3EE0                                      
        LDA #CPM                        ; COPYRIGHT MESSAGE
        JSR WRD5FV                                      
        LDX #$35E8                                      
        LDA #WEIM                                       
        JSR WRD5FV                                      
        LDA #HSCRM                      ; HI SCOR AND SECRETS
        LDX #$2BCC                                      
        JSR WRD5FV                                      
        LDX #$33C0                                      
        LDA #SECM                                       
        JSR WRD5FV                                      
CRDP0   LDX #NSHIP                                      
        JSR RCMOSD                                      
        STD PD1,U                                       
        LDB FREEPL+1                                    
        ANDB #$F                                        
        BEQ CRDP00                      ; NO FREE PLAY  
        CLR PD2,U                                       
CRDP00  LDB PD2,U                                       
        BNE CRDP1                       ; 2 CREDIT OPTION SELECTED
        LDX #$3858                                      
        BRA CRDP2                                       
CRDP1   LDX #$5958                                      
        TFR B,A                                         
        LDB #2                                          
        JSR CRDSB                                       
        LDA PD1,U                                       
        LDX #$1958                                      
CRDP2   LDB #1                                          
        JSR CRDSB                                       
CRDP3   LDA CREDIT                                      
        CMPA #1                                         
        BEQ CRDP3A                      ; NO TWO PLAYER, DONT ERASE
        JSR CRDWT                                       
        NAP (10,CRDP3A)                                 
CRDP3A  LDB #1                                          
        LDX #$3514                                      
        LDA #NPSM                                       
        JSR WRD7FV                                      
        LDA CREDIT                                      
        CMPA #2                                         
        BLO CRDP4                                       
        LDA PD2,U                                       
        BNE CRDP4A                                      
CRDP4   LDA PD1,U                                       
CRDP4A  CLRB                                            
        BSR CRDSB1                                      
        NAP ($80,CRDP5)                                 
CRDP5   LDA CREDIT                                      
        CMPA #2                                         
        BLO CRDP6                                       
        JSR CRDWT                                       
        NAP (10,CRDP50)                                 
CRDP50  LDB #2                                          
        LDX #$3514                                      
        LDA #NPSM                                       
        JSR WRD7FV                                      
        LDB PD1,U                                       
        LDA CREDIT                                      
        CMPA #4                                         
        BLO CRDP5A                                      
        TST PD2,U                                       
        BEQ CRDP5A                                      
        LDB PD2,U                                       
CRDP5A  TFR B,A                         ; SAME FOR BOTH PLAYERS
        BSR CRDSB1                                      
CRDP6   DEC PD3,U                       ; TIMER         
        LBEQ HALL                       ; TIME OUT      
        LDA CREDIT                                      
        CMPA PD4,U                                      
        BEQ CRDP7                                       
        STA PD4,U                                       
        LDA #08                                         
        STA PD3,U                                       
CRDP7                                                   
        NAP ($80,CRDP3)                                 
;                                                       
; CREDIT PAGE DISPLAY SUBROUTINE                                                
; A=#SHIPS,B=#CREDIT GAME                                                
CRDSB   PSHS A,X                                        
        LDA #NCGM                                       
        JSR WRD7FV                                      
        LDB ,S                                          
        LDX 1,S                                         
        LEAX $18,X                                      
        LDA #NSHM                                       
        JSR WRD7FV                                      
        LDX 1,S                                         
        LEAX $28,X                                      
        LDA #NSBM                                       
        JSR WRD7FV                                      
        LDX 1,S                                         
        LEAX $38,X                                      
        LDA #NIUM                                       
        JSR WRD7FV                                      
        PULS A,X,PC                                     
;                                                       
; PLAYER OBJECT DISPLAY SUBROUINTE                                                
; A=PLAYER 1 SHIP #,B=PLAYER 2                                                
;                                                       
CRDSB1  STA .P1LAS                                      
        STA .P1SBC                                      
        PSHS B                                          
        LDB #10                                         
        MUL                                             
        STB .P1INV                                      
        PULS B                                          
        STB .P2LAS                                      
        STB .P2SBC                                      
        LDA #10                                         
        MUL                                             
        STB .P2INV                                      
        JSR LDISP                                       
        JSR SBDISP                                      
        JMP IVDISP                                      
;                                                       
; CREDIT START MESS OFF                                                
;                                                       
CRDWT   LDD #0                                          
        BSR CRDSB1                                      
        LDX #$2E13                                      
        LDD #$3D08                                      
        JMP BLKCLR                                      
;                                                       
; OUTPUT EXTRA SHIP MESSAGE                                                
; X=ADDR                                                
OSEXM   PSHS X                                          
        LDX #REPLAY                                     
        JSR RCMOSB                                      
        TSTB                                            
        BEQ OSXMX                       ; NO REPLAYS    
        LDX ,S                                          
        LDA #SEXM                                       
        JSR WRD5FV                                      
OSXMX   PULS X,PC                                       
;                                                       
; OUTPUT CREDIT MESSAGE SUBROUTINE                                                
;                                                       
OCRDM   LDX #$3E40                                      
OCRDM1  LDB CREDIT                                      
        LDA #CREDM                                      
        JMP WRD7FV                                      
;                                                       
; SECRETS PAGE                                                
;                                                       
SECRET  LDA #$40                                        
        STA ATRFLG                                      
        LDA #$FF                                        
        STA STATUS                                      
        JSR GNCIDA                                      
        LDA #1                                          
        JSR DIMV                                        
        JSR SCRCLR                                      
        LDX #$0E10                                      
        LDD #$0801                                      
        JSR OTSGM                                       
        LDU CRPROC                                      
        LDA #SECSM                                      
        LDX #$4044                                      
        JSR WRD7FV                                      
        LDX #GA1+14                                     
        JSR RCMOSB                      ; GET WARPING PARAMS
        CLRA                                            
        TFR D,Y                                         
        JSR RCMOSB                                      
        LDA #SWRPM                                      
        JSR WRD7FV                                      
        LDD #$1111                                      
        STD GATCL1                                      
        LDD #$4444                                      
        STD GATCL2                                      
        LDD #$7777                                      
        STD GATCL3                                      
        LDD #$338F                                      
        JSR ONSG                                        
        LDX #$14CA                                      
        JSR OSEXM                                       
        LDX #$3EE0                                      
        JSR OCRDM1                                      
        MAKP (OPMCOL)                                   
        MAKP (LOGCOL)                                   
        MAKP (CBOMB)                                    
        LDU CRPROC                                      
        LDD #$1040                                      
        STD PD,U                                        
SECR0   DEC PD+1,U                                      
        BEQ SECX                        ; MAXIMUM TIMEOUT
        DEC PD,U                                        
        BEQ SECR1                                       
SEC55                                                   
        NAP (60,SECR0)                                  
SECR1   LDA PIA21                                       
        ANDA #$40                                       
        BEQ SECX                                        
        LDA #1                                          
        STA PD,U                                        
        BRA SEC55                                       
SECX    LDA CREDIT                                      
        BEQ HALL                                        
        JMP CRDPAG                                      
;                                                       
; STARGATE IMMORTALS HALL OF FAME                                                
;                                                       
HALL    LDA #$FF                                        
        STA STATUS                                      
        LDA #$66                                        
        STA SCNCOL                                      
        LDA #$10                                        
        STA ATRFLG                                      
        JSR GNCIDA                                      
        CLRA                            ; BECOME	A SUPER PROCESS
        LDX #HALL1                                      
        JSR MSPROC                                      
        JMP SUCIDE                                      
; ***********************                                                
; ***********************BEGINING OF LINKY INSERTION                                                
; ***********************                                                
;                                                       
HSY     equ $28                         ; Y OFFSET OF ENTIRE TABLE
HCOL1   equ $8                                          
;                                                       
HALL1   LDX #PCRAM                      ; CLEAR OUT COLORS
LHAL0   CLR ,X+                                         
        CMPX #PCRAM+16                                  
        BLO LHAL0                                       
        JSR PRINV                                       
        JSR SCRCLR                                      
        JSR SCLEAN                      ; KILL SAM STUFF
        MAKP (OPMCOL)                                   
        LDA #HSTABM                     ; PRINT BORING STUFF ON THE SCREEN
        JSR WRD7V                                       
        LDX #SGMSP                      ; MAKE A PROCESS TO OUTPUT THE STARGATE MESSAGE
        CLRA                                            
        JSR MSPROC                      ; DONE          
        LDA #$AA                                        
        STA TEXCOL                      ; MAKE GOD NAME VISIBLE
        LDX #GODSCR+38                  ; POINT AT LAST LETTER
LHAL1   JSR RCMOSA                      ; FETCH A LETTER
        LEAX -4,X                       ; UNDO INC, AND BACKUP 1
        CMPA #$3A                       ; SPACE??       
        BNE LHAL2                                       
        CMPX #GODSCR                                    
        BHS LHAL1                                       
LHAL2   LEAX 2,X                        ; POINT AT LAST 
        PSHS X                          ; PUT IT ON STACK
        LDX #$0E47                      ; GODS PLACE IN THE WORLD
        LDA #$31                                        
        JSR PR57V                                       
        LDA #$5C                                        
        JSR PR57V                                       
        LEAX $300,X                                     
        LDY #GODSCR                                     
LHAL3   EXG X,Y                                         
        JSR RCMOSA                      ; GET A CHARACTER
        EXG X,Y                                         
        JSR PR57V                       ; PRINT IT      
        CMPY ,S                         ; COMPARE TO LAST
        BLS LHAL3                       ; GOD NAME HAS BEEN PRINTED
        LEAX $200,X                     ; SLIDE OVER 8 PIXELS
        LDY #GODSCR+40                  ; POINT AT SCORE
        LDU #WRD7V                      ; PRINT IN 7 HIGH
        JSR PRSCOR                      ; PRINT GOD SCORE
        LEAX $500,X                                     
        LDA #$5B                                        
        JSR PR57V                                       
        LDY #GODINT                     ; POINT AT INITIALS
        LDB #3                                          
LHA3__  EXG X,Y                                         
        JSR RCMOSA                                      
        EXG X,Y                                         
        JSR PR57V                                       
        DECB                                            
        BNE LHA3__                                      
        LDA #$5C                                        
        JSR PR57V                                       
;                                                       
;  PD = CURRENT INDEX                                                
;  PD1= ENTRIES LEFT IN THIS COLUMN                                                
;  PD2= COLUMNS LEFT                                                
;  PD3= CURRENT X                                                
;  PD4= CURRENT Y                                                
;  PD5= POINTER TO CURRENT GUY IN CMOS                                                
;  PD7= # OF ENTRIES IN COLUMN                                                
;                                                       
        LDU CRPROC                                      
        LDA #$2                         ; START WITH INDEX 2
        STA PD,U                                        
        LDD #$0C56                                      
        STD PD+3,U                                      
        LDY #CMSCOR                                     
        STY PD+5,U                                      
        LDA #13                                         
        STA PD+7,U                                      
        JMP HSCOLM                      ; PRINT THIS MATRIX OF THE TABLE
PRTODY  LDU CRPROC                                      
        LDA #1                                          
        STA PD,U                        ; START WITH 1 FOR TODAYS
        LDD #$0CD0                                      
        STD PD+3,U                                      
        LDY #TODAYS                                     
        STY PD+5,U                                      
        LDA #2                                          
        STA PD+7,U                                      
        JMP HSCOLM                                      
HSCOLM  LDA #3                          ; 3 COLUMNS     
        STA PD+2,U                      ; SET IT HARD   
        CLR TXFLAV                                      
HSCOL0  LDA PD+7,U                      ; NUMBER PER COLUMN
        STA PD+1,U                      ; MAKE THAT NUMBER LEFT IN THIS COLUMN
HSCOL3                                                  
        NAP (1,HSCOL6) TIME BREAK HERE                ; TIME BREAK HERE
HSCOL6  LDA TEXCOL                      ; GET CURRENT COLOR
        ADDA #$11                       ; PUSH TO NEXT  
        CMPA #$88                                       
        BLS HSCOL1                                      
        LDA #$11                                        
HSCOL1  STA TEXCOL                                      
        LDX PD+3,U                      ; GET ADDRESS   
        LDB PD,U                        ; GET PLAYERS INDEX
        LDA #HSINDX                     ; INDEX MESSAGE 
        JSR WRD5V                       ; PRINT IT      
        LDY PD+5,U                      ; POINT AT PLAYERS INITIALS
        LDB #3                          ; ITERATE 3 TIMES
HSCOL2  EXG X,Y                                         
        JSR RCMOSA                      ; GET AN INITIAL
        EXG X,Y                                         
        JSR PR35V                       ; PRINT IT OUT  
        DECB                                            
        BNE HSCOL2                      ; INITIALS ARE OUT THERE
        LDX PD+3,U                      ; GET CURSOR POINTER AGAIN
        LEAX $E00,X                     ; POSITION FOR SCORE
        LDU #WRD5V                      ; USE 3 BY 5 SCORE ROUTINE
        JSR PRSCOR                      ; PRINT OUT THE GUYS SCORE
        LDU CRPROC                                      
        LDY PD+5,U                      ; GET CMOS POINTER
        LEAY SCRSIZ,Y                                   
        STY PD+5,U                                      
        LDX PD+3,U                      ; GET CURRENT CURSOR
        LEAX 7,X                        ; MOVE 7 PIXELS 
        STX PD+3,U                                      
        LDA PD,U                        ; KICK INDEX    
        ADDA #1                                         
        DAA                                             
        STA PD,U                                        
        DEC PD+1,U                      ; ONE LESS GUY IN THIS COLUMN
        BNE HSCOL3                                      
        LEAX $3000,X                    ; MOVE TO NEXT COLUMN
        LDA #7                          ; HEIGHT PER ENTRY
        LDB PD+7,U                      ; ENTRIES PER COLUMN
        MUL                                             
        NEGB                            ; OPPOSITE OF TOTAL HEIGHT
        LEAX B,X                        ; TO RESTORE THAT HEIGHT
        STX PD+3,U                      ; RESTORED      
        DEC PD+2,U                      ; ONE LESS COLUMN
        BNE HSCOL0                      ; DO ANOTHER    
        CMPY #TODAYS+SCRSIZ                ; ARE WE IN TODAYS
        LBLS PRTODY                     ; NOPE...GO AND DO TODAYS
;                                                       
PRDONE                                                  
        MAKP (LOGCOL) DONE...START THE COLORS.                ; DONE...START THE COLORS.
        LDU CRPROC                                      
        LDA #8                                          
        STA PD,U                                        
HALX1   DEC PD,U                                        
        BEQ HALX                                        
        NAP (60,HALX1)                                  
HALX    LDA #120                                        
        STA PD,U                                        
HALX2   LDA PIA21                                       
        ANDA #$8                                        
        BEQ LOGO                                        
        DEC PD,U                                        
        BEQ LOGO                                        
        NAP (30,HALX2)                                  
SGMSP   LDX #$0E10                      ; TOP OF SCREEN 
        LDD #$0A01                                      
        JSR OTSGM                       ; PUT IT OUT    
        JMP SUCIDE                      ; AND DIE       
PRSCOR  PSHS Y                          ; SAVE Y FOR THE HELL OF IT
        EXG X,Y                         ; SWAP EM       
        JSR RCMOSA                                      
        JSR RCMOSB                                      
        EXG X,Y                                         
        ANDA #$0F                                       
        BNE PR2SC                       ; THIS WILL BE A TWO CALL JOB
        TSTB                                            
        BNE PR2SC                       ; THIS WILL BE A ONE CALL JOB
        LDA #HIGH3                      ; USE OTHER ROUTINE
        BRA PR3SC                                       
PR2SC   PSHS Y                          ; SAVE NEW Y    
        TFR D,Y                         ; DATA TO PRINT 
        LDA #HIGH1                      ; USE FIRST PRINT TEXT
        JSR ,U                          ; CALL PASSED ROUTINE TO DO IT
        PULS Y                                          
        LDA #AUDM2                      ; USE AUDIT TEXT TO PUT OUT LOW HALF
PR3SC   PSHS A                                          
        EXG X,Y                                         
        JSR RCMOSA                                      
        JSR RCMOSB                                      
        EXG X,Y                                         
        TFR D,Y                                         
        PULS A                                          
        JSR ,U                                          
        PULS Y,PC                       ; AND RETURN    
; ***********************                                                
; ***********************END OF LINKY INSERTION                                                
; ***********************                                                
LOGO    JSR GNCIDA                                      
        LDA #$20                                        
        STA ATRFLG                      ; LOGO PROGRAM  
        LDA #1                                          
        JSR DIMV                        ; DIM DOWN, CHAPS
        JSR SCRCLR                                      
        MAKP (OPMCOL)                                   
;                                                       
; OUTPUT OPERATOR MESSAGE                                                
; PD=MESS PTR,PD2=SCREEN ADDR,PD4=BYTE CNT.,PD5=PASS FLAG                                                
OOM     CLR PD6,U                                       
        JSR ATMCK                       ; MESSAGE O.K.???
        BEQ OOM0                                        
        LDX #OMST                                       
        INC PD6,U                       ; ERROR FLAG    
        BRA OOM00                                       
OOM0    LDX #OPMESS                                     
OOM00   STX PD,U                                        
        LDX #OPL1                       ; LOCATION OF LINE 1
        LDB #$20                                        
        JSR GETMEX                                      
        STX PD2,U                                       
        LDA #25                                         
        STA PD4,U                                       
        STA PD5,U                                       
        LDA #$AA                                        
        STA TEXCOL                                      
        CLR TXFLAV                                      
OOML    LDX PD,U                                        
        LDA PD6,U                       ; ERROR?        
        BEQ OOML0                       ; NO            
        LDA ,X+                                         
        BRA OOML00                                      
OOML0   JSR RCMOSA                                      
OOML00  STX PD,U                                        
        LDX PD2,U                                       
        JSR PR57V                                       
        STX PD2,U                                       
        DEC PD4,U                                       
        BEQ OOFIN                                       
OOML1                                                   
        NAP (3,OOML)                                    
OOFIN   LDA PD5,U                                       
        BEQ OOFX                                        
        CLR PD5,U                                       
        LDA #25                                         
        STA PD4,U                                       
        LDX #OPL2                                       
        LDB #$30                                        
        JSR GETMEX                                      
        STX PD2,U                                       
        CLR TXFLAV                                      
        BRA OOML1                                       
OOFX    equ *                                           
        MAKP (COLSHK)                                   
        LDX #$0E58                                      
        LDD #$0102                                      
        JSR OTSGM                                       
        NAP (40,LOG20)                                  
LOG20   JSR GNCIDA                                      
        CLR PCRAM                                       
        MAKP (OPMCOL)                                   
        MAKP (LOGCOL)                                   
        LDA #CPM                                        
        LDX #$3498                                      
        JSR WRD7FV                                      
        LDA #WEIM                                       
        LDX #$25A8                                      
        JSR WRD7FV                                      
        LDX #$3ABC                                      
        JSR OCRDM1                      ; OUTPUT CREDIT MESSAGE
        LDA #HSCRM                                      
        LDX #$28CC                                      
        JSR WRD5FV                                      
        LDA #SECM                                       
        LDX #$30D4                                      
        JSR WRD5FV                                      
        NAP (40,LOG2)                                   
LOG2    equ *                                           
        LDX PD+10,U                     ; GET START ADDRESS
        LEAY $8824,X                                    
        LDA #7                          ; SET COLOR 9 TO RED
        STA PCRAM+9                                     
        LDA #$99                        ; AND DO HALO IN COLOR 9
        JSR HALO                                        
        NAP ($FF,PIKVCT) DELAY 4 SECS AND GO TO LOOP                ; DELAY 4 SECS AND GO TO LOOP
; OPERATOR DEFAULT MESSAGE                                                
OMST    FCC "WILLIAMS ELECTRONICS INC"                                
        FCB $3D                         ; PERIOD        
        FCC "           PRESENTS"                                
        FCB $3F                         ; COLON         
        FCC "     "                                     
;                                                       
GETMEX  LDA #$25                        ; ASSUME ERROR  
        TST PD+6,U                                      
        BNE GETME3                                      
        JSR RCMOSA                      ; GET THE BYTE  
        CMPA #$10                                       
        BHS GETME2                                      
        LDA #$10                                        
GETME2  CMPA #$3C                                       
        BLS GETME3                                      
        LDA #$3C                                        
GETME3  TFR D,X                                         
        RTS                                             
;                                                       
; OPERATOR MESS COLORER                                                
;                                                       
OPMCOL  INC PD,U                                        
        LDA PD,U                                        
        ANDA #$7                                        
        LDX #OPMTAB                                     
        LDA A,X                                         
        STA PCRAM+$A                                    
        NAP (5,OPMCOL)                                  
OPMTAB  FCB $7,$87,$FF,$FF,$C7,$C7,$FF,$FF                                
;                                                       
; LOGO CYCLER                                                
; PD,U=COLOR INDEX                                                
;                                                       
LOGCOL  LDX #LCTAB                                      
        STX PD,U                                        
LOGLP   LDX PD,U                                        
        LDY #PCRAM+1                                    
LOGL0   LDA ,X+                                         
        BNE LOGL1                                       
        LDX #LCTAB                                      
        BRA LOGL0                                       
LOGL1   STA ,Y+                                         
        CMPY #PCRAM+9                                   
        BNE LOGL0                                       
        LDX PD,U                                        
        LEAX 1,X                                        
        LDA ,X                                          
        BNE LOGL2                                       
        LDX #LCTAB                                      
LOGL2   STX PD,U                                        
        NAP (3,LOGLP)                                   
;                                                       
; LOGO COLORING TABLE                                                
;                                                       
LCTAB   FCB $37,$2F,$27,$1F,$17,$47,$47,$87                                
        FCB $87,$C7,$C7,$C6,$C5,$CC,$CB,$CA                                
        FCB $C0,$D0,$98,$38,$33,0,0,0                                
        FCB 0,0,0,0,0,0,0,0                                
        FCB 0,0,0,0,0,0,0,0                                
;                                                       
; COLOR SHAKER                                                
;                                                       
COLSHK  LDX #SHKTAB                                     
        JSR RAND                                        
        ANDA #$F                                        
        LDA A,X                                         
        STA PCRAM                                       
        LDA #5                                          
        LDX #COLSH1                                     
        JMP SLEEP                                       
COLSH1  CLR PCRAM                                       
        NAP (3,COLSHK)                                  
SHKTAB  FCB $7,$F,$17,$1E,$25,$2C,$2B,$2A                                
        FCB $28,$60,$D0,$C0,$C3,$85,$47,$87                                
;                                                       
; OUTPUT STARGATE                                                
; SUPER PROCESS                                                
; PD+8=TABLE X,PD+40=RET ADD,PD10=SADDR                                                
; A=COLS/DELAY;X=SADD;B=DELAY/SLEEP                                                
OTSGM   STX PD+10,U                     ; START ADDR    
        STA PD1,U                       ; COLS/DELAY    
        STB PD+13,U                                     
        PULS X                                          
        STX PD+14,U                     ; RET ADDR      
        LDX #SGMTAB                                     
        STX PD+8,U                                      
        CLR PD+12,U                                     
OTSGM1  LDX PD+8,U                                      
        LDY ,X++                        ; LETTER ADDR   
        BEQ OTSGMX                      ; THE END       
        LDA ,X+                                         
        STD PD2,U                       ; COLOR         
        LDA ,X+                                         
        STA PD,U                        ; FLAVOR MASK   
        LDD ,X++                        ; SCREEN ADDR OFFSET
        ADDD PD+10,U                                    
        STX PD+8,U                                      
        TFR D,X                                         
        JMP OUTLET                                      
OTSGMX  JMP [PD+14,U]                   ; RETURN        
;                                                       
; OUTPUT LETTER                                                
; X=SCREEN ADDR TOP LEFT                                                
; Y=LETTER DATA TABLE                                                
; PD=FLAVOR MASKL;PD1=DELAY;PD3=REPCNT                                                
; PD12=DELAY TEMP                                                
OUTLET  CLR PD3,U                                       
        STX PD4,U                                       
OUTL0   LDA ,Y                                          
        BEQ OUTLX                       ; END           
        BPL OUTL1                                       
        BITA #$40                                       
        BNE OUTL1                                       
        ANDA #$3F                                       
        STA PD3,U                                       
        LEAY 1,Y                                        
        BRA OUTL0                                       
OUTL1   PSHS Y                                          
        JSR OUTCOL                                      
        LDA PD3,U                                       
        BEQ OUTL2                                       
        DEC PD3,U                                       
        BEQ OUTL2                                       
        LDY ,S                                          
OUTL2   LEAS 2,S                                        
        COM PD,U                                        
        BPL OUTL2A                                      
        INC PD4,U                                       
OUTL2A  STY PD6,U                                       
        DEC PD+12,U                                     
        BPL OUTL3                                       
        LDA PD1,U                                       
        STA PD+12,U                                     
        LDX #OUTL3                                      
        LDA PD+13,U                                     
        JMP SLEEP                                       
OUTL3   LDX PD4,U                                       
        LDY PD6,U                                       
        BRA OUTL0                                       
OUTLX   JMP OTSGM1                                      
;                                                       
; OUTPUT COLUMN                                                
; X=ADDR TOP LEFT                                                
; Y=DATA ADDR                                                
; PD,U=FLAVOR MASK ;PD2,U=COLOR                                                
;                                                       
OUTCOL  PSHS D                                          
OUTC1   LDA ,Y+                                         
        PSHS A                          ; END OF COLUMN FLAG
        BITA #$40                                       
        BNE OUTC2                                       
        ANDA #$3F                                       
        LEAX A,X                                        
        BRA OUTCX                                       
OUTC2   ANDA #$3F                                       
        BEQ OUTCX                                       
OUTC3   LDB PD,U                                        
        ANDB PD2,U                                      
        ORCC #$10                       ; SEI           
        CLR RWCNTL                      ; ENABLE RAM READ
        ORB ,X                                          
        STB ,X+                                         
        LDB #1                                          
        STB RWCNTL                                      
        ANDCC #$EF                      ; (CLI)         
        DECA                                            
        BNE OUTC3                                       
OUTCX   LDA ,S+                                         
        BPL OUTC1                                       
        PULS D,PC                                       
;                                                       
;  HALO ROUTINE---THIS ROUTINE PUTS OUT A HALO AROUND THE EDGES OF                                                
;  ALL SOLID REGIONS IN A DEFINED AREA.                                                
;                                                       
;  X = UPPER LEFT CORNER OF REGION                                                
;  Y = LOWER RIGHT CORNER (JUST OUTSIDE OF REGION)                                                
;  A = COLOR OF HALO                                                
;                                                       
HALO    STY XTEMP                       ; SAVE BOTTOM RIGHT IN MEMORY
        PULS Y                          ; GET RETURN ADDRESS
        LDU CRPROC                                      
        STX PD,U                        ; SAVE UPPER LEFT
        STY PD+6,U                      ; RETURN ADDRESS
        CLRB                            ; START	ON EVEN FLAVOR
HALO0   BSR VFILL                       ; FILL A VERTICAL LINE
        TSTB                            ; MOVE	TO NEXT FLAVOR
        BEQ HALO1                       ; EVEN FLAVOR..MOVE TO ODD
        LDU CRPROC                                      
        STD PD+2,U                                      
        STX PD+4,U                                      
        NAP (1,HALO50)                                  
HALO50  LDD PD+2,U                                      
        LDX PD+4,U                                      
        LEAX $100,X                     ; ODD FLAVOR MOVE TO NEXT BYTE
        LDB #$FF                        ; SETUP FOR EVEN FLAVOR
HALO1   INCB                            ; TO NEXT FLAVOR
        EXG X,D                         ; SEE IF DUN    
        CMPA XTEMP                      ; DONE IF X BEYOND OR EQUAL TO PASSED Y
        EXG X,D                                         
        BLO HALO0                       ; WE STILL IN RANGE...DO ANOTHER COLUMN
;                                                       
        LDU CRPROC                                      
        LDX PD,U                        ; RESTORE ORIGINAL CURSOR
HALO2   LDA #$4B                        ; HALF A LINE DOWN
        STA XTEMP                                       
        JSR HFILL                       ; DO A HORIZONTAL ROW
        STX PD+4,U                                      
        NAP (1,HAL_55)                                  
HAL_55  LDA #$8C                                        
        STA XTEMP                                       
        LDB PD+5,U                                      
        LDA #$4B                                        
        TFR D,X                                         
        JSR HFILL                                       
        NAP (1,HALO55)                                  
HALO55  LDX PD+4,U                                      
        LEAX 1,X                        ; MOVE TO NEXT ROW
        EXG X,D                                         
        LDA #$0E                                        
        CMPB XTEMP+1                    ; TOO MUCH Y?   
        EXG X,D                                         
        BLO HALO2                       ; NOT THIS PASS 
        LDU CRPROC                                      
        JMP [PD+6,U]                    ; RETURN        
VFILL   PSHS X,D                        ; SAVE POINTER, COLOR, FLAVOR
VFILL1  BSR WCHK                        ; SEE IF THERE IS WHITE OR BLACK
        BNE VFILL2                      ; ITS WHITE....BOUNDARY FOUND
        BSR VADV                        ; DO AN ADVANCE TO NEXT PIXEL
        BRA VFILL1                                      
VFILL2  BSR VON                         ; TURN ON THE CURRENT PIXEL
        BSR VADV                        ; PUSH TO NEXT ONE
        BSR VON                         ; TURN IT ON TOO
VFILL3  BSR WCHK                        ; LOOK FOR BLACK NOW...WE IN A WHITE REGION
        BEQ VFILL4                      ; FOUND SOME BLACK
        BSR VADV                        ; NEXT PIX      
        BRA VFILL3                                      
VFILL4  LEAX -2,X                       ; MOVE BACK 2 TO WHITEN
        BSR VON                         ; TURN EM ON    
        LEAX 1,X                                        
        BSR VON                                         
        LEAX 1,X                        ; BACK TO BLACK PIX THAT TRIGGERED IT
        BRA VFILL1                                      
WCHK    PSHS D                          ; SEE IF CURRENT POINTED TO IS ON OR OFF
        ORCC #$10                       ; (SEI) WE HAVE TO READ THE SCREEN
        CLR RWCNTL                                      
        BSR GETMSK                      ; GET THE RIGHT MASK
        ANDA ,X                                         
        LDB #1                                          
        STB RWCNTL                      ; UNCONTROL     
        ANDCC #$EF                      ; (CLI)         
        TSTA                            ; SET FLAGS FOR RETURN
        PULS D,PC                                       
GETMSK  LDA #$F0                        ; ASSUME EVEN   
        TSTB                                            
        BEQ GETMX                       ; YEP           
        LDA #$0F                        ; ODD           
GETMX   RTS                                             
VADV    LEAX 1,X                        ; PUSH TO NEXT PIXEL
        EXG X,D                         ; COMPARE       
        CMPB XTEMP+1                    ; IS OUR Y ENOUGH??
        EXG X,D                                         
        BLO GETMX                       ; DONE..IN RANGE...RETURN
HADV2   LEAS 2,S                        ; REMOVE CALL TO US
        PULS X,D,PC                     ; AND RETURN FOR VFILL
VON     PSHS D                          ; SAVE          
        ORCC #$10                       ; (SEI) READ SCREEN
        CLR RWCNTL                                      
        BSR GETMSK                      ; GET THE CURRENT MASK
        ANDA ,X                         ; READ THE SCREEN
        BEQ VBYE                        ; NOTHING THERE....DONT SHADE.
        BSR GETMSK                      ; GET MASK BACK 
        ANDA #$99                       ; MASK COLOR ON 
        PSHS A                          ; PUT THAT HALF OF BYTE UP
        BSR GETMSK                      ; ANOTHER MASK PLEASE
        COMA                            ; OTHER HALF    
        ANDA ,X                         ; GET WHATS ON SCREEN.
        ORA ,S+                         ; OR IN OTHER HALF
        STA ,X                          ; AND STORE     
VBYE    LDA #1                                          
        STA RWCNTL                                      
        ANDCC #$EF                      ; (CLI)         
        PULS D,PC                                       
HFILL   PSHS X,D                                        
        CLRB                            ; START	ON EVEN FLAVOR
HFILL1  BSR WCHK                                        
        BNE HFILL2                      ; STILL LOOKING FOR WHITE
        BSR HADV                        ; MOVE TO NEXT PIX
        BRA HFILL1                                      
HFILL2  BSR VON                         ; TURN ONE ON   
        BSR HADV                        ; MOVE TO NEXT  
        BSR VON                         ; TURN ONE ON   
HFILL3  BSR WCHK                                        
        BEQ HFILL4                                      
        BSR HADV                                        
        BRA HFILL3                                      
HFILL4  LEAX -$100,X                    ; MOVE BACK 2 PIXELS
        BSR VON                                         
        BSR HADV                        ; MOVE 1 PIX FWD
        BSR VON                                         
        BSR HADV                                        
        BRA HFILL1                                      
HADV    TSTB                                            
        BEQ HADV1                                       
        LEAX $100,X                     ; TO NEXT BYTE  
        EXG X,D                                         
        CMPA XTEMP                      ; SEE IF TOO MUCH
        EXG X,D                                         
        BHS HADV2                                       
        LDB #$FF                                        
HADV1   INCB                                            
        RTS                                             
;                                                       
;                                                       
; STARGATE MESSAGE TABLE                                                
; LETTER,COLOR,DELAY,FLAVOR,X16                                                
SGMTAB  equ *                                           
; LETTER S                                                
        FDB SGS                                         
        FCB $11,$F0                                     
        FDB $0000                                       
; LETTER T                                                
        FDB SGT                                         
        FCB $22,$F0                                     
        FDB $1000                                       
; LETTER A                                                
        FDB SGA                                         
        FCB $33,$F0                                     
        FDB $1F00                                       
; LETTER R                                                
        FDB SGR                                         
        FCB $44,$F0                                     
        FDB $2F00                                       
; LETTER G                                                
        FDB SGG                                         
        FCB $55,$F0                                     
        FDB $3F00                                       
; LETTER A                                                
        FDB SGA                                         
        FCB $66,$F0                                     
        FDB $4F00                                       
; LETTER T                                                
        FDB SGT                                         
        FCB $77,$F0                                     
        FDB $5F00                                       
; LETTER E                                                
        FDB SGE                                         
        FCB $88,$F0                                     
        FDB $6F00                                       
        FDB 0                           ; END           
;                                                       
; STARGATE LETTER DATA                                                
;                                                       
; SSSSSSS                                                
;                                                       
SGS     FCB 6,$44,$9,$D0                                
        FCB 4,$48,7,$D0                                 
        FCB 3,$4A,$6,$D0                                
        FCB 2,$4C,$5,$D0                                
        FCB $82                         ; REPEAT TWICE  
        FCB 1,$4E,$4,$D0                                
        FCB $8D                         ; REPEAT 13     
        FCB $50,$3,$D0                                  
        FCB $82                         ; REPEAT TWICE  
        FCB $50,4,$CE                                   
        FCB $50,5,$CC                                   
        FCB $50,6,$CA                                   
        FCB $50,7,$C8                                   
        FCB $50,9,$C4                                   
        FCB 0                           ; END OF LETTER 
; TTTTTTTTTTTTTTTTTTTTTTTT                                                
SGT     FCB $87                         ; REPEAT 7      
        FCB $CA                                         
        FCB $8A                         ; REPEAT 10     
        FCB $4A,$2,$D7                                  
        FCB $87                                         
        FCB $CA                                         
        FCB 0                                           
; LETTER A                                                
SGA     FCB 15,$40+2,2,$C0+16                                
        FCB 13,$40+4,2,$C0+16                                
        FCB 11,$40+6,2,$C0+16                                
        FCB 9,$40+8,2,$C0+16                                
        FCB 7,$40+10,2,$C0+16                                
        FCB 5,$40+12,2,$C0+16                                
        FCB 3,$40+14,2,$C0+16                                
        FCB 1,$40+16,2,$C0+16                                
        FCB $80+2                       ; REPEAT 2 TIMES
        FCB $40+17,2,$C0+16                                
        FCB $80+5                       ; REPEAT 5 TIMES
        FCB $40+12,7,$C0+5                                
        FCB $80+2                       ; REPEAT 2 TIMES
        FCB $40+17,2,$C0+16                                
        FCB 1,$40+16,2,$C0+16                                
        FCB 3,$40+14,2,$C0+16                                
        FCB 5,$40+12,2,$C0+16                                
        FCB 7,$40+10,2,$C0+16                                
        FCB 9,$40+8,2,$C0+16                                
        FCB 11,$40+6,2,$C0+16                                
        FCB 13,$40+4,2,$C0+16                                
        FCB 15,$40+2,2,$C0+16                                
        FCB 0                           ; END OF LETTER A
;  START OF LETTER R                                                
SGR     FCB $80+10                      ; REPEAT 10 TIMES
        FCB $C0+35                                      
        FCB $C0+0                       ; BLANK COLUMN  
        FCB $C0+0                       ; BLANK COLUMN  
        FCB $80+4                       ; REPEAT 4 TIMES
        FCB $40+22,2,$C0+11                                
        FCB $80+2                       ; REPEAT 2 TIMES
        FCB 1,$40+21,2,$C0+11                                
        FCB $80+2                       ; REPEAT 2 TIMES
        FCB 2,$40+20,2,$C0+11                                
        FCB 3,$40+19,4,$C0+9                                
        FCB 4,$40+17,7,$C0+7                                
        FCB 5,$40+15,10,$C0+5                                
        FCB 7,$40+11,14,$C0+3                                
        FCB 9,$40+7,18,$C0+1                                
        FCB 0                           ; END OF LETTER R
;  START OF LETTER G                                                
SGG     FCB 12,$C0+11                                   
        FCB 10,$C0+15                                   
        FCB 8,$C0+19                                    
        FCB 7,$C0+21                                    
        FCB 5,$C0+24                                    
        FCB 4,$C0+26                                    
        FCB 3,$C0+28                                    
        FCB 2,$C0+30                                    
        FCB 2,$C0+31                                    
        FCB 1,$40+15,3,$C0+14                                
        FCB 1,$40+13,7,$C0+13                                
        FCB $40+12,11,$C0+12                                
        FCB $80+2                       ; REPEAT 2 TIMES
        FCB $40+11,13,$C0+11                                
        FCB $80+2                       ; REPEAT 2 TIMES
        FCB $40+10,15,$C0+10                                
        FCB $80+9                       ; REPEAT 9 TIMES
        FCB $40+10,2,$C0+23                                
        FCB 0                           ; END OF LETTER G
;  START OF LETTER E                                                
SGE     FCB $80+9                       ; REPEAT 9 TIMES
        FCB $40+10,2,$C0+23                                
        FCB $80+15                      ; REPEAT 15 TIMES
        FCB $40+10,2,$40+10,3,$C0+10                                
        FCB 0                           ; END OF LETTER E
;  FILLER TO MATCH PROMS                                                
        org 55632                                       
        FCB $8E,$8E,$8E,$8E,$8E,$8E,$8E,$8E,$8E                                
